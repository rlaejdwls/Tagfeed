apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compile_sdk_version.toInteger()
    buildToolsVersion rootProject.build_tools_version
    flavorDimensions "tagfeed"

    defaultConfig {
        applicationId "kr.co.treegames.tagfeed"
        minSdkVersion rootProject.min_sdk_version.toInteger()
        targetSdkVersion rootProject.target_sdk_version.toInteger()
        versionCode 1
        versionName "0.0.01"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    buildTypes {
        debug {
            debuggable true

            buildConfigField "boolean", "CRASHLYTIS", "true"
            buildConfigField "boolean", "ANALYTICS", "true"
            buildConfigField "boolean", "PERFORMANCE", "true"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "CRASHLYTIS", "true"
            buildConfigField "boolean", "ANALYTICS", "false"
            buildConfigField "boolean", "PERFORMANCE", "true"
        }
    }
    productFlavors {
        //Flavors
        tagfeed {
            dimension "tagfeed"
            manifestPlaceholders = [ appLabel: "@string/app_title" ]
        }
        beta {
            dimension "tagfeed"
            applicationIdSuffix ".beta"
            manifestPlaceholders = [ appLabel: "@string/app_title" ]
        }
    }
    android.variantFilter { variant ->
        if((variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'beta')) {
            variant.setIgnore(true)
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.productFlavors[0].name}-${variant.versionName}-" +
                    "${variant.versionCode}-${variant.variantData.variantConfiguration.buildType.name}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    //Local
    implementation fileTree(include: ['*.jar'], dir: 'libs')
//    implementation project(':core')
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.23.0"
    implementation "com.android.support.test.espresso:espresso-idling-resource:3.0.2"
    androidTestImplementation "org.mockito:mockito-android:2.8.47"
    androidTestImplementation 'com.android.support.test:runner:1.1.0'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    //Support
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation 'com.google.android:flexbox:1.1.0'
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    //Google
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-perf:16.2.0'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.google.firebase:firebase-config:16.1.0'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-ml-vision:18.0.1'
    implementation 'com.google.firebase:firebase-firestore:17.1.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'
    //Animation
    implementation 'com.airbnb.android:lottie:2.8.0'
    //ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
//    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0'
    //JSON
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    //Database
    implementation "androidx.room:room-runtime:2.0.0"
    implementation "androidx.room:room-rxjava2:2.0.0"
//    annotationProcessor "android.arch.persistence.room:compiler:2.0.0"
    kapt "androidx.room:room-compiler:2.0.0"
}

apply plugin: 'com.google.gms.google-services'